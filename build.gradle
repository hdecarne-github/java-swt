// Plugins
plugins {
	id "de.carne.java-tools" version "0.7.1" apply false
	id "org.sonarqube" version "2.7" apply false
	id "com.jfrog.artifactory" version "4.9.0" apply false
	id "com.jfrog.bintray" version "1.8.4" apply false
}

// Build helpers
def snapshotBuild = project.version.endsWith("-SNAPSHOT")

def resolveProperty(propertyName, envName) {
	def resolved = (project.findProperty(propertyName) ?: System.getenv(envName));

	if(resolved == null) {
		throw new GradleException("Missing property/environment variable: '${propertyName}'/'${envName}'")
	}
	return resolved; 
}

// Projects
subprojects {

	apply plugin: "java-library"
	apply plugin: "jacoco"
	apply plugin: "de.carne.java-tools"
	apply plugin: "org.sonarqube"
	apply plugin: "maven-publish"
	apply plugin: "com.jfrog.artifactory"
	apply plugin: "com.jfrog.bintray"
	apply plugin: "eclipse"
	
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	
	repositories {
		if(snapshotBuild) {
			mavenLocal()
			maven { url "https://oss.jfrog.org/libs-snapshot/" }
		}
		jcenter()
		maven { url "https://oss.jfrog.org/libs-release/" }
	}
	
	def projectBaseName = (project.name.startsWith("java-swt") ? "java-swt" : "java-test-swt")
	def sharedSrcDir = "../${projectBaseName}-gtk-linux-x86_64/shared-src"

	sourceSets {
		main {
			java {
				srcDir "${sharedSrcDir}/main/java"
			}
			resources {
				srcDir "${sharedSrcDir}/main/resources"
			}
		}
		test {
			java {
				srcDir "${sharedSrcDir}/test/java"
			}
			resources {
				srcDir "${sharedSrcDir}/test/resources"
			}
		}
	}
	
	javatools {
		generateI18N {
			enabled = true
			genDir = file("${sharedSrcDir}/main/java")
			bundles = fileTree("${sharedSrcDir}/main/resources") {
				include "**/*I18N.properties"
			}
			lineSeparator = "\n"
		}
	}

	def platformSuffix = project.name.substring(projectBaseName.length())
	def swtPlatformSuffix = platformSuffix.replace("-", ".")

	dependencies {
		compileOnly(group: "org.eclipse.jdt", name: "org.eclipse.jdt.annotation", version: project.annotationVersion)
		testCompileOnly(group: "org.eclipse.jdt", name: "org.eclipse.jdt.annotation", version: project.annotationVersion)
		api(group: "de.carne.common", name: "java-default", version: project.javaDefaultVersion)
		api(group: "org.eclipse.platform", name: "org.eclipse.swt${swtPlatformSuffix}", version: project.swtVersion) {
			exclude(group: "org.eclipse.platform", module: 'org.eclipse.swt.${osgi.platform}')
		}
		if(projectBaseName.equals("java-swt")) {
			testImplementation(group: "de.carne.common", name: "java-test", version: project.javaTestVersion)
			testImplementation(group: "org.jmockit", name: "jmockit", version: project.jmockitVersion)
		} else {
			api(group: "de.carne.common", name: "java-test", version: project.javaTestVersion)
			api(group: "org.jmockit", name: "jmockit", version: project.jmockitVersion)
			implementation project(":" + project.name.replace("java-test-swt", "java-swt"))
		}
	}
	
	jar {
		manifest {
			attributes "Automatic-Module-Name": (projectBaseName.equals("java-swt") ? automaticModuleName : automaticTestModuleName)
		}
		// Exclude TestApp from artifact
		exclude("/de/carne/test/swt/app/**")
		exclude("/META-INF/de.carne.boot.Application")
	}

	test {
		useJUnitPlatform()
		// SWT display can only be created once per VM
		forkEvery = 1
		testLogging {
			events "failed"
			exceptionFormat "full"
		}
		jvmArgs "-javaagent:${classpath.find { it.name.contains('jmockit') }.absolutePath}"
		enabled = project.name.endsWith(javatools.platform.swtToolkit)
	}

	project.tasks["sonarqube"].enabled = test.enabled

	jacoco {
		toolVersion = "0.8.2"
	}
		
	jacocoTestReport {
		reports {
			xml.enabled false
			html.enabled true
			html.destination file("${buildDir}/reports/jacoco")
			csv.enabled false
		}
	}
	
	task sourceJar(type: Jar) {
		from sourceSets.main.allJava
		classifier "sources"
	}
	
	task javadocJar(type: Jar) {
		from javadoc
		classifier = "javadoc"
	}
	
	publishing {
		publications {
			Bintray(MavenPublication) {
				from components.java
				artifact sourceJar
				artifact javadocJar
				pom {
					name = project.name
					description = projectDescription
					url = projectUrl
					licenses {
						license {
							name = projectLicense
							url = projectLicenseUrl
							distribution = "repo"
						}
					}
					developers {
						developer {
							id = project.resolveProperty("developerId", "DEVELOPER_ID")
							name = project.resolveProperty("developerName", "DEVELOPER_NAME")
							email = project.resolveProperty("developerEmail", "DEVELOPER_EMAIL")
						}
					}
					scm {
						url = projectScmUrl
					}
				}
			}
		}
	}

	artifactory {
		contextUrl = "https://oss.jfrog.org/artifactory"
		publish {
			repository {
				repoKey = (snapshotBuild ? "oss-snapshot-local" : "oss-release-local")
				username = project.resolveProperty("artifactory_user", "ARTIFACTORY_USER")
				password = project.resolveProperty("artifactory_password", "ARTIFACTORY_PASSWORD")
				maven = true
			}
			defaults {
				publications("Bintray")
			}
		}
		resolve {
			repoKey = (snapshotBuild ? "libs-snapshot" : "libs-release")
			username = project.resolveProperty("artifactory_user", "ARTIFACTORY_USER")
			password = project.resolveProperty("artifactory_password", "ARTIFACTORY_PASSWORD")
			maven = true
		}
		clientConfig.setIncludeEnvVars(false)
	}
	
	bintray {
		user = project.resolveProperty("bintrayUser", "BINTRAY_USER")
		key = project.resolveProperty("bintrayKey", "BINTRAY_KEY")
		publications = [ "Bintray" ]
		pkg {
			repo = "maven"
			name = project.name
			licenses = [ projectLicenseId ]
			vcsUrl = projectScmUrl
			version {
				gpg {
					sign = true
					passphrase = project.resolveProperty("bintrayGpgPassphrase", "BINTRAY_GPG_PASSPHRASE")
				}
			}
		}
	}

	eclipse {
		classpath {
			downloadSources = true
			downloadJavadoc = true
			file {
				whenMerged {
					def eeaPath = project.findProperty("eclipse.eeaPath");
					 
					entries.forEach {
						if(it.path.startsWith("org.eclipse.jdt.launching.JRE_CONTAINER")) {
							if(eeaPath != null) {
								it.entryAttributes.put("annotationpath", eeaPath)
							}
						} else if(it.path.contains("/caches/modules-2/")) {
							if(eeaPath != null) {
								it.entryAttributes.put("annotationpath", eeaPath)
							}
						}
					}
				}
			}
		}
	}
}
