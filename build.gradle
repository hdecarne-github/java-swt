// Plugins
plugins {
	id "de.carne.java-tools" version "0.6.5" apply false
	id "org.sonarqube" version "2.6.2" apply false
	id "com.jfrog.artifactory" version "4.7.5" apply false
	id "com.jfrog.bintray" version "1.8.4" apply false
}

// Build helpers
def snapshotBuild = project.version.endsWith("-SNAPSHOT")

def resolveProperty(propertyName, envName) {
	def resolved = (project.findProperty(propertyName) ?: System.getenv(envName));
	
	if(resolved == null) {
		throw new GradleException("Missing property/environment variable: '${propertyName}'/'${envName}'")
	}
	return resolved; 
}

// Projects
subprojects {

	apply plugin: "java-library"
	apply plugin: "jacoco"
	apply plugin: "de.carne.java-tools"
	apply plugin: "org.sonarqube"
	apply plugin: "maven-publish"
	apply plugin: "com.jfrog.artifactory"
	apply plugin: "com.jfrog.bintray"
	apply plugin: "eclipse"
	
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	
	repositories {
		if(snapshotBuild) {
			mavenLocal()
			maven { url "https://oss.jfrog.org/libs-snapshot/" }
		}
		jcenter()
		maven { url "https://oss.jfrog.org/libs-release/" }
	}
	
	def sharedSrcDir = "../java-test-swt-gtk-linux-x86_64/shared-src"

	sourceSets {
		main {
			java {
				srcDir "${sharedSrcDir}/main/java"
			}
		}
		test {
			java {
				srcDir "${sharedSrcDir}/test/java"
			}
			resources {
				srcDir "${sharedSrcDir}/test/resources"
			}
		}
	}
	
	def platformSuffix = project.name.substring(rootProject.name.length())
	def swtPlatformSuffix = platformSuffix.replace("-", ".")

	dependencies {
		api(group: "de.carne.common", name: "java-default", version: "8.7.0-SNAPSHOT")
		api(group: "org.eclipse.platform", name: "org.eclipse.swt${swtPlatformSuffix}", version: "3.108.0") {
			exclude(group: "org.eclipse.platform", module: 'org.eclipse.swt.${osgi.platform}')
		}
		
		implementation(group: "org.junit.jupiter", name: "junit-jupiter-api", version: "5.3.1")
		testRuntimeOnly(group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "5.3.1")
	}
	
	jar {
		manifest {
			attributes "Automatic-Module-Name": automaticModuleName
		}
	}
	
	test {
		useJUnitPlatform()
		// SWT display can only be created once per VM
		forkEvery = 1
		testLogging {
			events "failed"
			exceptionFormat "full"
		}
	}

	jacoco {
		toolVersion = "0.8.2"
	}
		
	jacocoTestReport {
		reports {
			xml.enabled false
			html.enabled true
			html.destination file("${buildDir}/reports/jacoco")
			csv.enabled false
		}
	}
	
	task sourceJar(type: Jar) {
		from sourceSets.main.allJava
		classifier "sources"
	}
	
	task javadocJar(type: Jar) {
		from javadoc
		classifier = "javadoc"
	}
	
	publishing {
		publications {
			Bintray(MavenPublication) {
				from components.java
				artifact sourceJar
				artifact javadocJar
				pom {
					name = project.name
					description = projectDescription
					url = projectUrl
					licenses {
						license {
							name = projectLicense
							url = projectLicenseUrl
							distribution = "repo"
						}
					}
					developers {
						developer {
							id = project.resolveProperty("developerId", "DEVELOPER_ID")
							name = project.resolveProperty("developerName", "DEVELOPER_NAME")
							email = project.resolveProperty("developerEmail", "DEVELOPER_EMAIL")
						}
					}
					scm {
						url = projectScmUrl
					}
				}
			}
		}
	}

	artifactory {
		contextUrl = "https://oss.jfrog.org/artifactory"
		publish {
			repository {
				repoKey = (snapshotBuild ? "oss-snapshot-local" : "oss-release-local")
				username = project.resolveProperty("artifactory_user", "ARTIFACTORY_USER")
				password = project.resolveProperty("artifactory_password", "ARTIFACTORY_PASSWORD")
				maven = true
			}
			defaults {
				publications("Bintray")
			}
		}
		resolve {
			repoKey = (snapshotBuild ? "libs-snapshot" : "libs-release")
			username = project.resolveProperty("artifactory_user", "ARTIFACTORY_USER")
			password = project.resolveProperty("artifactory_password", "ARTIFACTORY_PASSWORD")
			maven = true
		}
		clientConfig.setIncludeEnvVars(false)
	}
	
	bintray {
		user = project.resolveProperty("bintrayUser", "BINTRAY_USER")
		key = project.resolveProperty("bintrayKey", "BINTRAY_KEY")
		publications = [ "Bintray" ]
		pkg {
			repo = "maven"
			name = project.name
			licenses = [ projectLicenseId ]
			vcsUrl = projectScmUrl
			version {
				gpg {
					sign = true
					passphrase = project.resolveProperty("bintrayGpgPassphrase", "BINTRAY_GPG_PASSPHRASE")
				}
			}
		}
	}

	eclipse {
		classpath {
			downloadSources = true
			downloadJavadoc = true
		}
	}
}
